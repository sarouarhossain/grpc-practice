plugins {
	id 'java'
	id 'org.springframework.boot' version '2.4.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'com.google.protobuf' version '0.8.15'
	id 'idea'
}

group = 'com.demo'
version = '0.0.1-SNAPSHOT'


configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:3.12.0"
	}
	plugins {
		grpc {
			artifact = 'io.grpc:protoc-gen-grpc-java:1.37.0'
		}
	}
	generateProtoTasks {
		all().each { task ->
			task.builtins {
				java { option 'lite' }
			}
			task.plugins {
				grpc { option 'lite' }
			}
		}
	}
}

dependencies {
	//implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	//implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'net.devh:grpc-server-spring-boot-starter:2.11.0.RELEASE'
	implementation 'io.grpc:grpc-netty-shaded:1.37.0'
	implementation 'io.grpc:grpc-protobuf:1.37.0'
	implementation 'io.grpc:grpc-stub:1.37.0'
	compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	//runtimeOnly 'mysql:mysql-connector-java'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

// if you have source imports issues, add the below
sourceSets.main.java.srcDir new File(buildDir, 'generated/source')
idea {
	module {
		// Marks the already(!) added srcDir as "generated"
		generatedSourceDirs += file('build/generated/source')
	}
}

test {
	useJUnitPlatform()
}
